ACTOR SteamSpawner : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Steam Spawner
	//$Color 12

	//$Arg0 "Direction"
	//$Arg0Tooltip "0 is up, 1 is down, 2 is forward (downfacing spawners should be placed 4 map units below the ceiling)"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Up"; 1 = "Down"; 2 = "Forward"; }
	//$Arg1 "Type"
	//$Arg1Tooltip "Second argument chooses if it's a constant spawner, or spawns in bursts. Burst spawners use the third argument and play a different sound."
	//$Arg1Type 11
	//$Arg1Enum { 0 = "Constant"; 1 = "Bursts"; }
	//$Arg2 "Frequency"
	//$Arg2Tooltip "Third argument controls the frequency of the bursts (divided by 16. I.e, 128 will end up as 8). This is only used by the second argument."
	//$Arg3 "Sound"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Yes"; 1 = "No"; }

	Height 2
	Radius 1
	Mass 0
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Active:
	  TNT1 A 0 A_ChangeFlag ("DORMANT", FALSE) //mxd. DORMANT flag must be updated manually
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SteamSwitch")==0,"EndSpawn")
		TNT1 A 0 A_JumpIf(Args[0] == 1, "Down")
		TNT1 A 0 A_JumpIf(Args[0] > 1, "Forward")
		TNT1 A 0 A_JumpIf(Args[1] > 0, "UpBurst")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 3)
		TNT1 A 0 A_PlaySound("STEAM_SMALL", CHAN_7, 1.0, TRUE)
		BaseLoop:
		TNT1 A 1 A_SpawnItemEx("SteamParticle", 0, 0, 1, (0.1)*random(0, 4), 0, (0.1)*random(35, 40), random(0, 360), 128)
		Loop
	Down:
		TNT1 A 0 A_JumpIf(Args[1] > 0, "DownBurst")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 3)
		TNT1 A 0 A_PlaySound("STEAM_SMALL", CHAN_7, 1.0, TRUE)
		DownLoop:
		TNT1 A 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*random(0, 4), 0, (-0.1)*random(35, 40), random(0, 360), 128)
		Loop
	Forward:
		TNT1 A 0 A_JumpIf(Args[1] > 0, "ForwardBurst")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 3)
		TNT1 A 0 A_PlaySound("STEAM_SMALL", CHAN_7, 1.0, TRUE)
		ForwardLoop:
		TNT1 A 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*random(35, 40), 0, (0.1)*random(-5, 5), random(-8, 8), 128)
		Loop
	UpBurst:
		TNT1 A 1 A_Jump(Args[2]/16, 1)
		Loop
		TNT1 A 0 A_JumpIf(Args[3] > 0, 3)
		TNT1 A 0 A_PlaySound("STEAM_BURST", CHAN_7, 1.0, TRUE)
		UpBLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 1, (0.1)*random(0, 4), 0, (0.1)*random(35, 40), random(0, 360), 128)
		Loop
	DownBurst:
		TNT1 A 1 A_Jump(Args[2]/16, 1)
		Loop
		TNT1 A 0 A_JumpIf(Args[3] > 0, 3)
		TNT1 A 0 A_PlaySound("STEAM_BURST", CHAN_7, 1.0, TRUE)
		DownBLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*random(0, 4), 0, (-0.1)*random(35, 40), random(0, 360), 128)
		Loop
	ForwardBurst:
		TNT1 A 1 A_Jump(Args[2]/16, 1)
		Loop
		TNT1 A 0 A_JumpIf(Args[3] > 0, 3)
		TNT1 A 0 A_PlaySound("STEAM_BURST", CHAN_7, 1.0, TRUE)
		ForwBLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("SteamParticle", 0, 0, 0, (0.1)*random(35, 40), 0, (0.1)*random(-5, 5), random(-8, 8), 128)
		Loop
	Inactive:
		TNT1 A 0 A_ChangeFlag ("DORMANT", TRUE) //mxd. DORMANT flag must be updated manually
		TNT1 A -1 A_StopSound(CHAN_7)
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	}
}

ACTOR SteamParticle
{
	Height 1
	Radius 1
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+WINDTHRUST
	Scale 0.04
	Alpha 0.65
	States
	{
	Spawn:
		STEM A 0 
		"####" A 2 A_SetScale(scalex+0.013, scaley+0.013)
		"####" A 0 A_FadeOut(.04)
		Loop
	}
}