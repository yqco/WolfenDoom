ACTOR WaterSplashGeneratorNormal : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Water Splash Generator (normal)
	//$Color 12

	//$Arg3 "Sound"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Yes"; 1 = "No"; }
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
		TNT1 A 0 A_PlaySound("water/lap", CHAN_7, 1.0, FALSE)
		TNT1 AAAAAAA 2 A_SpawnItemEx("WaterSplashSpawner")
	Inactive:
		TNT1 A -1
		Loop
	}
}

ACTOR WaterSplashSpawner : RandomSpawner
{
	DropItem "WaterSplashObject1"
	DropItem "WaterSplashObject2"
	DropItem "WaterSplashObject3"
	DropItem "WaterSplashObject4"
}

ACTOR WaterSplashObject1
{
	Mass 100
	Gravity 6.0
	Alpha 0.8
	Scale 0.3
	-NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		WTSA A 0 NODELAY
		"####" A 0 ThrustThingZ(0,random(4,8),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" A 1 A_SetScale(scalex+0.01, scaley+0.01)
		"####" A 0 A_FadeOut(.025)
		Goto Spawn+3
	}
}

ACTOR WaterSplashObject2 : WaterSplashObject1 
{
	States
	{
		Spawn: 
			WTSB A 0 NODELAY
			Goto Super::Spawn+1
	}
}

ACTOR WaterSplashObject3 : WaterSplashObject1
{
	States
	{
		Spawn: 
			WTSB A 0 NODELAY
			Goto Super::Spawn+1
	}
}
ACTOR WaterSplashObject4 : WaterSplashObject1 
{
	States
	{
		Spawn: 
			WTSC A 0 NODELAY
			Goto Super::Spawn+1
	}
}

ACTOR WaterSplashCloud
{
	Mass 100
	Gravity 6.0
	Alpha 0.0
	Scale 0.7
	-NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		WTFG A 0 NODELAY
		"####" A 0 ThrustThingZ(0,random(1,4),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" AAAAAAAAA 2 A_FadeIn(.03)
		"####" A 2 A_SetScale(scalex+0.01, scaley+0.01)
		"####" A 0 A_FadeOut(.03)
		Goto Spawn+12
	}
}

ACTOR WaterSplashGeneratorNormalLooping : WaterSplashGeneratorNormal
{
	//$Category Special Effects (Wolf3D)
	//$Title Water Splash Generator (normal, looping)
	//$Color 12

	States
	{
	Spawn:
	Active:
		TNT1 A 4 A_SpawnItemEx("WaterSplashSpawner")
		TNT1 A 4 A_SpawnItemEx("WaterSplashCloud", random(-8,8), random(-8,8), random(0,16))
		Loop
	Inactive:
		TNT1 A -1
		Loop
	}
}