ACTOR Bubble
{
	Radius 2
	Height 2
	Speed 1
	Scale 0.05
	Alpha 0.25
	RenderStyle "Add"
	Projectile
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOCLIP
	States
	{
	Spawn:
		SBUB A 1
	Rise:
		SBUB A 0 A_JumpIf(waterlevel == 0, "Death")
		SBUB A 1 A_Jump (32, "Jiggle")
		Loop
	Jiggle:
		SBUB A 0 A_JumpIf(waterlevel == 0, "Death")
		SBUB A 1 A_SetAngle (random (0, 360))
		SBUB A 0 A_Recoil (0.1)
		Goto Rise
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR PlayerBubble : Bubble
{
	States
	{
	Spawn:
		SBUB A 1 NODELAY A_ChangeVelocity(0,0,random(1,3),CVF_RELATIVE)
		Goto Rise
	}
}

ACTOR BubbleSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Underwater Bubble Spawner
	//$Color 12
	Radius 2
	Height 2
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("BubbleSwitch")==0,"EndSpawn")
		TNT1 A 0 A_CheckSightOrRange(1280,"Unsighted") //needed for more tweaks
		TNT1 A 2 A_SpawnItemEx("Bubble", random (-16, 16), 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	EndSpawn:
		TNT1 A 1
		Stop
	Unsighted:
		TNT1 A 25
		Goto Spawn
	}
}