#include "zcommon.acs"

//Map Setup
script 100 OPEN
{
    thing_activate(34);
}

//Open first large gold portal
script 101 (void)
{
    SetLineActivation(25, SPAC_None);
    changefloor(26, "SW_ON_11");
    ceiling_raisebyvalue(24, 15, 240);
}

//Spike Fire Room 1
script 102 (int fireburst5)
{
	SpawnProjectile(36,"Nail",64,90,0,0,0);
	delay(17);
	SpawnProjectile(37,"Nail",192,90,0,0,0);
	delay(17);
 while(fireburst5<10)
 {
            Thing_Projectile(152, 222, random(1,255), 1, -60);
            Thing_Projectile(154, 222, random(1,255), 1, -60);
            delay(random(2,4));
            fireburst5++;
 }
 fireburst5=0;
	restart;
}

//Chaindoor open
int firstchain=0;

script 103 (int chainnumber)
{
    door_open(chainnumber, 16);

    if(firstchain<1)
    {
        firstchain++;
    }
    else
    {
        ceiling_raisebyvalue(57, 16, 128);
    }
}

//Raise water
int raisewatercounter=0;

script 104 (void)
{

    ceiling_raisebyvalue(62, 8, 78);
    delay(20);
    thing_activate(63+raisewatercounter);
    ThrustThingZ(63, 64, 0, 1);
    raisewatercounter++;
}

//Open blue door
script 105 (void)
{
    floor_lowerbyvalue(65, 2, 32);
    ceiling_raisebyvalue(66, 8, 128);
}

//Blue Key Trap
script 106 (void)
{
    Polyobj_MoveTo(1, 16, 9400, 4960);
    floor_lowerbyvalue(68, 16, 88);
}

//Spike Puzzle
int spikepuzzlestart=79;

script 107 (void)
{
        thing_deactivate(spikepuzzlestart);

        while(spikepuzzlestart<88)
        {
            delay(1*35);
            thing_deactivate(spikepuzzlestart+1);
            delay(1*35);
            thing_activate(spikepuzzlestart);
            spikepuzzlestart++;
        }
        spikepuzzlestart=79;
        thing_activate(88);
        restart;
}

//Raise platforms in water area
script 108 (int platposition)
{
    ceiling_raisebyvalue(platposition, 16, 280);
    floor_raisebyvalue(platposition, 16, 280);
}

//3 Switches before lava
int lavaswitches=0;

script 109 (void)
{
    lavaswitches++;
    if(lavaswitches==3)
    {
        ceiling_raisebyvalue(114, 8, 64);
        floor_raisebyvalue(114, 8, 64);
    }

}

//Spikes
script 110 (int fireburst5)
{
 SpawnProjectile(119,"NailFlame",0,90,0,0,0);
	delay(15);
 SpawnProjectile(120,"NailFlame",192,90,0,0,0);
	delay(15);
 while(fireburst5<10)
    {
            Thing_Projectile(152, 222, random(1,255), 1, -60);
            Thing_Projectile(154, 222, random(1,255), 1, -60);
            delay(random(2,4));
            fireburst5++;
    }
 fireburst5=0;
 restart;
}

//Chaindoor 2 open
int secondchain=0;

script 111 (int chainnumber)
{
    door_open(chainnumber, 16);

    if(secondchain<1)
    {
        secondchain++;
    }
    else
    {
        Polyobj_MoveTo(11, 4, 16640, 9768);
        Thing_activate(139);
    }
}

//Battle cultists against nazis
script 112 (void)
{
    thing_activate(133);
    thing_activate(78);
    thing_hate(78, 133, 3);
    thing_hate(133, 78, 3);

}

//End Script
script 113 (void) //Ending Sequence
{
	SetPlayerProperty(1,1,PROP_FROZEN);
    acs_terminate(192, 0);
	setmusic("music/D_INTER.ogg");
	SetHudSize(480,320,1);
	SetFont("BLACKBG");
	hudmessagebold(s: "a"; HUDMSG_FADEINOUT,14,CR_WHITE,240.0,160.0,999.0,3.0,0.0);
	delay(3*35);
	SetFont("ENDC2M0");
	hudmessagebold(s: "a"; HUDMSG_FADEINOUT,13,CR_WHITE,240.0,160.0,999.0,2.0,0.0);
	delay(2*35);
 SetPlayerProperty(1,0,PROP_FROZEN);
	SetHudSize(640,480,1);
	SetFont("SMALLFONT");
	hudmessagebold(s: "Once more \cAGeneral Fettgesicht\cC was quicker \nbut this time he's more thrifty with his words.\n Instantly one of his henchmen hits you with his rifleshoulder. \nThe first time isn't enough and half-conscious, you can\nhear the Nazis activate the portal. It's more \nefficient the second time though..."; HUDMSG_TYPEON,11,CR_WHITE,170.1,120.1,3.0,0.05,2.0);
	delay(3*35);
	hudmessagebold(s: "Press <use> to skip the sequence"; HUDMSG_FADEINOUT,12,CR_GRAY,320.0,460.0,3.0,1.0,1.0);

	int buttons;
	while (TRUE)
	{
		buttons = GetPlayerInput(-1,INPUT_BUTTONS);
		if (buttons & BT_USE)
		{
			SetHudSize(480,320,1);
			SetFont("BLACKBG");
			hudmessagebold(s: "a"; HUDMSG_FADEINOUT,10,CR_WHITE,240.0,160.0,999.0,2.0,0.0);
			delay(2*35);
			exit_normal(0);
		}
		delay(1);
	}
}

//////////////////////////////////////
////// Generic Effect Scripts ////////
//////////////////////////////////////

script 402 ENTER
//Setup the player
{
        GiveInventory("Luger9mm",1);
        GiveInventory("9mmAmmo",8);
        GiveInventory("KnifeSilent",1);
        GiveInventory("TrenchShotgun", 1);
        GiveInventory("12GaugeAmmo", 4);
        SetWeapon("TrenchShotgun");
}

//Spark Spawner Script
script 403 OPEN
{
	thing_activate(42);
	delay(random(10,20));
	thing_deactivate(42);
	delay(random(35,350));
	restart;
}

//Generic vent breaker script
script 404 (int vent_id)
{
	SetLineBlocking(vent_id,BLOCK_NOTHING);
}

//Generic Flush Sound WC
script 405 (int toilet_id)
{
	PlaySound(toilet_id,"toilet/flush",CHAN_BODY,1.0,0,0);
}

//Generic Mirror Script
script 406 (int line_id)
{
	TranslucentLine(line_id,255);
	GlassBreak(0);
	SetLineTexture(line_id,SIDE_FRONT,TEXTURE_MIDDLE,"MIRR_W04");
}

//Water Waggle & Plane Reflection
script 407 OPEN
{
	//Ceiling_Waggle(30,32,16,0,0);
	Sector_SetPlaneReflection(128,20,0);
}

//Generic Light Script
script 408 (int light_tid)
{
	if(!GetActorProperty(light_tid,APROP_Dormant))
		Thing_Deactivate(light_tid);
	else
		Thing_Activate(light_tid);
	PlaySound(0,"SW_LIGHT");
}

//Generic Ladder Script
script 409 (void)
{
    ThrustThingZ(0, 15, 0, 0);
    ACS_NamedExecute("Laddersound", 0, 0, 0, 0);
}

script "Laddersound" (void)
{
    PlaySound (0, "world/ladder", CHAN_BODY, 0.71);
    delay(15);
}

////////////////
// OBJECTIVES //
////////////////

script "OBJECTIVES" OPEN
{
	//Set primary objectives
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,0,"MO_ICON_OPN");
	ACS_NamedExecuteAlways("boaobjectivesset",0,1,0,"MO_C2M0A_P1");
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,1,"MO_ICON_OPN");
	ACS_NamedExecuteAlways("boaobjectivesset",0,1,1,"MO_C2M0A_P2");
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,2,"MO_ICON_OPN");
	ACS_NamedExecuteAlways("boaobjectivesset",0,1,2,"MO_C2M0A_P3");
	//Set secondary objectives
	ACS_NamedExecuteAlways("boaobjectivesset",0,1,3,"MO_C2M0A_S1");
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,3,"MO_ICON_OPN");
}


script 200 (void)
{
	//Prim Objective 1 solved
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,0,"MO_ICON_ACC");	
	ACS_NamedExecuteAlways("boaobjectiveaccomplished",0,0,0,0);
}

script 201 (void)
{
	//Prim Objective 2 solved
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,1,"MO_ICON_ACC");	
	ACS_NamedExecuteAlways("boaobjectiveaccomplished",0,0,0,0);
}

script 202 (void)
{
	//Prim Objective 3 solved
	ACS_NamedExecuteAlways("boaobjectivesset",0,0,2,"MO_ICON_ACC");	
	ACS_NamedExecuteAlways("boaobjectiveaccomplished",0,0,0,0);
}
